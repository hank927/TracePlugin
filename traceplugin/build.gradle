buildscript {
    repositories {
        maven { url 'https://maven.aliyun.com/nexus/content/groups/public/' }
        mavenCentral()
    }
}

plugins {
    id 'groovy'
    id "java"
    id "java-gradle-plugin"
    id "com.gradle.plugin-publish" version "0.14.0"
}

apply plugin: 'com.github.dcendents.android-maven'

group 'com.github.hank927'
version '1.0.3'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation 'com.android.tools.build:gradle:3.5.0'
    implementation 'org.javassist:javassist:3.20.0-GA'
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks.withType(Javadoc) {
    options.encoding = "UTF-8"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}



task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

// Use java-gradle-plugin to generate plugin descriptors and specify plugin ids
gradlePlugin {
    plugins {
        greetingsPlugin {
            id = 'com.github.hank927.traceplugin'
            implementationClass = 'com.hank.trace.MTracePlugin'
        }
    }
}

// The configuration example below shows the minimum required properties
// configured to publish your plugin to the plugin portal
pluginBundle {
    website = 'https://github.com/hank927/TracePlugin'
    vcsUrl = 'https://github.com/hank927/TracePlugin'
    description = 'a library for android apps to trace the method cost'
    tags = ['method', 'trace']

    plugins {
        greetingsPlugin {
            // id is captured from java-gradle-plugin configuration
            displayName = 'method trace gradle plugin'
        }
    }

    mavenCoordinates {
        groupId = 'com.github.hank927'
        artifactId = 'traceplugin'
        version = '1.0.3'
    }
}
